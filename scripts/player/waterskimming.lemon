function void Character.checkForWaterSurface()
{
#if GAMEAPP
	if (A0 == 0xffffb000 && (!level.water_present || (s16(char.position.y.u16) <= s16(water.height.current))))
	{
		//Game.setUnderwaterAudioEffect(0)
	}
#endif

	if (!level.water_present)
		return

	bool wasUnderwaterBefore = (char.flags & char.flag.UNDERWATER) != 0
	bool isUnderwaterNow = (s16(char.position.y.u16) > s16(water.height.current))
	if (isUnderwaterNow == wasUnderwaterBefore)
		return

	++water.transition_counter
	A1 = A0
	UpdateMusicOnWaterTransition()

	bool isSuperActive = super.active
	bool controlFlag = (char.control_flags == 0)
	if (char.character == CHARACTER_TAILS)
	{
		isSuperActive = super.active.tails
		controlFlag = (tails.ai_routine == 4) || (char.control_flags == 0)
	}

	if (isUnderwaterNow)
	{
		char.flags |= char.flag.UNDERWATER

		// Spawn underwater update object
		if (char.character == CHARACTER_TAILS)
		{
			u32[0xffffcb76] = 0x018164
			u8[0xffffcba2] = 0x81
			u32[0xffffcbb6] = A0
		}
		else
		{
			u32[0xffffcb2c] = 0x018164
			u8[0xffffcb58] = 0x81
			u32[0xffffcb6c] = A0
		}

		if (controlFlag)
		{
			char.velocity.x >>= 1
			char.velocity.y >>= 2
			if (char.velocity.y != 0)
			{
				if (char.jumping == 0) && (abs(char.groundspeed) >= 0x0600) && (char.state == char.state.ROLLING)
				{
					char.velocity.y = -0x200

					if (char.flags & char.flag.FACING_LEFT)
					{
						char.groundspeed = char.groundspeed + 0x200
					}
					else
					{
						char.groundspeed = char.groundspeed - 0x200
					}
				}

				// Set state of dust cloud object to 0x01
				u16[A6 + 0x20] = 0x0100
				playSound(SFX_WATERSPLASH)
			}
		}

	#if STANDALONE
		// Update achievement
		if (A0 == 0xffffb000)
		{
			Game.setAchievementValue(ACHIEVEMENT_LBZ_STAY_DRY, 0)
		}
	#endif
	}
	else
	{
		char.flags &= ~char.flag.UNDERWATER

		if (controlFlag && char.base_state != 0x04)	// That's the "got hurt" state
		{
			if (char.velocity.y >= -0x400)
			{
				char.velocity.y *= 2
			}
		}

		if (char.state != char.state.DISAPPEARED && char.velocity.y != 0)
		{
			// Set state of dust cloud object to 0x01
			u16[A6 + 0x20] = 0x0100
			char.velocity.y = max(char.velocity.y, -0x1000)
			playSound(SFX_WATERSPLASH)
		}
	}

	// Update physics
	setSpeedCapProperties(A4)
}