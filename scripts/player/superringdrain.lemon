//# address-hook(0x011b26) end(0x011bdc)
function void Character.UpdateRingsInSuperForm()
{
	// Any super form active?
	bool isSuperActive = (char.character == CHARACTER_TAILS) ? super.active.tails : super.active
	if (!isSuperActive)
		return

	// This dirty flag is only set when game progress is not paused (think of cutscenes)
	if (hud.dirty.timer)
	{
		// Countdown the frames until next ring gets consumed
		--super.ring_dec.frames
		if (super.ring_dec.frames >= 0)
			return

		// Consume next ring
		if (super.active & 0x80 || super.active.tails & 0x80) && (global.zone_act != 0x0c00)
		{
			super.ring_dec.frames = 45
		}
		else
		{
			super.ring_dec.frames = 60
		}

		if (ring_counter != 0)
		{
			hud.dirty.rings |= 0x01	// Possibly GUI ring update?
			if (ring_counter == 1 || ring_counter == 10 || ring_counter == 100)
			{
				hud.dirty.rings |= 0x80	// Update all digits
			}

			--ring_counter
			if (ring_counter != 0)
				return
		}
	}

	Character.cancelSuperTransformation()

#if STANDALONE
	if (global.zone != 0x0c)	// No music change in DDZ
	{
		chooseFittingMusic()
	}
#endif
}
